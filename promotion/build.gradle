version = '0.1.0'

dependencies {
    compileOnly 'foundation.icon:javaee-api:0.9.0'

	//there is a bug with this library, it can not be deployed in goloop becuase scorex defines a StringBuffer(int) and it is not supported in icon:javaee-rt
	//so i changed locally and installed in my local env, we need to ask the dev to patch it or even open a pr to the public repo
	//implementation 'com.github.sink772:javaee-scorex:0.5.3'
	implementation 'com.github.sink772:javaee-scorex:0.5.2'
	implementation 'com.github.sink772:minimal-json:0.9.6'

    testImplementation 'foundation.icon:javaee-unittest:0.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
	testImplementation project(':reward-distribution')
	testImplementation project(':testinteg')
	testImplementation 'foundation.icon:icon-sdk:2.0.0'
	testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.3.0'
}

optimizedJar {
    mainClassName = 'com.iconbet.score.promotion.Promotion'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

deployJar {
    endpoints {
        local {
            uri = 'http://localhost:9082/api/v3'
            nid = 0x3
        }
    }
    keystore = rootProject.hasProperty('keystoreName') ? "$keystoreName" : ''
    password = rootProject.hasProperty('keystorePass') ? "$keystorePass" : ''
}

test {
    useJUnitPlatform()
}

sourceSets {
	integrationTest {
		java.srcDirs = [file('src/intTest/java')]

		compileClasspath += sourceSets.main.output + configurations.testRuntime
		runtimeClasspath += output + compileClasspath
	}
}

configurations {
  integrationTestCompile.extendsFrom testImplementation
  integrationTestRuntime.extendsFrom testRuntime
  integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

//def tapTokenJar = project(':tap-token').getTasks().getByName('optimizedJar')
def rewardDistributionJar = project(':reward-distribution').getTasks().getByName('optimizedJar')

task integrationTest(type: Test, dependsOn: optimizedJar) {
	useJUnitPlatform()
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	testLogging.showStandardStreams = true

	// use the common config files
	systemProperty('env.props', new File(project(':testinteg').projectDir, 'conf/env.props'))

	def prefix = 'score.path.'
	systemProperty(prefix + project.name, optimizedJar.outputJarName)
	//systemProperty(prefix + 'tap-token', tapTokenJar.outputJarName.substring(0, tapTokenJar.outputJarName.lastIndexOf("-optimized.jar") )+ "-"+project.version+"-optimized.jar" )
	systemProperty(prefix + 'reward-distribution', rewardDistributionJar.outputJarName.substring(0, rewardDistributionJar.outputJarName.lastIndexOf("-optimized.jar") )+ "-"+project.version+"-optimized.jar" )

}

integrationTest.dependsOn(rewardDistributionJar)